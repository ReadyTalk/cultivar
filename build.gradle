defaultTasks 'build'

subprojects {
  apply plugin: 'java'
  apply plugin: 'checkstyle'
  apply plugin: 'findbugs'
  apply plugin: 'jacoco'
  
  defaultTasks 'build'
  
  project.ext.libVersions = [
    curator: "2.4.2",
    slf4j: "1.7.7",
    logback: "1.1.2",
    findbugs: "2.0.3",
    junit: "4.10",
    guice: "3.0",
    metrics: "3.0.2",
    guava: "17.0",
    mockito: "1.9.5",
  ]
  
  sourceSets {
    integTest {
      compileClasspath += main.output + test.output
      runtimeClasspath += main.output + test.output
    }
  }
  
  configurations {
    integTestCompile {
      extendsFrom testCompile, compile
    }
  
    integTestRuntime.extendsFrom testRuntime
  }
  
  repositories {
    mavenCentral()
  }
  
  group = 'org.readytalk.cultivar'
  version = '1.0.0-SNAPSHOT'
  
  dependencies {
    compile (
      "javax.inject:javax.inject:1",
      "com.google.code.findbugs:jsr305:${libVersions.findbugs}",
      "com.google.code.findbugs:annotations:${libVersions.findbugs}",
      "org.slf4j:slf4j-api:${libVersions.slf4j}",
      "com.google.guava:guava:${libVersions.guava}",
    )
    
    testCompile (
      "junit:junit:${libVersions.junit}",
      "org.mockito:mockito-all:${libVersions.mockito}",
      "ch.qos.logback:logback-classic:${libVersions.logback}",
    )
  }
  
  checkstyle {
    toolVersion = '5.7'
    showViolations = true
    sourceSets = [sourceSets.main]
    configFile = file("$rootDir/config/checkstyle/checkstyle.xml")
    ignoreFailures = false
  }
      
  findbugs {
    toolVersion = "${libVersions.findbugs}"
    sourceSets = [sourceSets.main]
    ignoreFailures = false
  }
  
  tasks.withType(FindBugs) {
    reports {
      xml.enabled = false
      html.enabled = true
    }
  }
  
  jacocoTestReport {
    reports {
      html.enabled = true
      csv.enabled = true
      xml.enabled = true
    } 
  }
  
  task integTest(type: Test) {
    testClassesDir = sourceSets.integTest.output.classesDir
    classpath = sourceSets.integTest.runtimeClasspath
  
    testReportDir = file("$buildDir/reports/integTests")
  
    jacoco {
      destinationFile = file("$buildDir/jacoco/test.exec")
      append = true
    }
  }
  
  jacocoTestReport.dependsOn(test)
  integTest.dependsOn test
  build.dependsOn javadoc
  build.dependsOn jacocoTestReport
  build.dependsOn integTest
  
  jacocoTestReport.mustRunAfter integTest
  
  tasks.withType(Compile) { 
    options.compilerArgs << "-Xlint:all" 
  }
}

wrapper {
  gradleVersion '1.11'
}
